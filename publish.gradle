if (project.ext.has("libraryPublishEnabled") && project.ext.libraryPublishEnabled) {
//    apply plugin: 'bintray-release'
    apply plugin: 'com.github.panpf.bintray-publish'

    publish {
        artifactId = releaseArtifact
        desc = releaseDescription
        publishVersion = versionName
        repoName = "OnePlayer"
        userOrg = 'coopsrc'
        groupId = 'com.coopsrc.oneplayer'
        website = 'https://github.com/coopsrc'
        bintrayUser = System.getenv('BINTRAY_USER')
        bintrayKey = System.getenv('BINTRAY_KEY')
        dryRun = false
    }

    gradle.taskGraph.whenReady { taskGraph ->
        project.tasks.findAll {
            task -> task.name.contains("generatePomFileFor")
        }.forEach { task ->
            task.doLast {
                task.outputs.files.filter {
                    File file ->
                        file.path.contains("publications") && file.name.matches("^pom-.+\\.xml\$")
                }.forEach {
                    File file -> addLicense(file)
                }
            }
        }
    }
}

static void addLicense(File pom) {
    def licenseNode = new Node(null, "license")
    licenseNode.append(new Node(null, "name", "The Apache Software License, Version 2.0"))
    licenseNode.append(new Node(null, "url", "http://www.apache.org/licenses/LICENSE-2.0.txt"))
    licenseNode.append(new Node(null, "distribution", "repo"))
    def licensesNode = new Node(null, "licenses")
    licensesNode.append(licenseNode)

    def xml = new XmlParser().parse(pom)
    xml.append(licensesNode)

    def writer = new PrintWriter(new FileWriter(pom))
    writer.write("<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n")
    def printer = new XmlNodePrinter(writer)
    printer.preserveWhitespace = true
    printer.print(xml)
    writer.close()
}